#include "decorate/base/scripts.txt"
#include "decorate/base/puppetmaster.txt"
#include "decorate/base/possessed.txt"

#include "decorate/PlasmaGunner.txt"
#include "decorate/RailGunner.txt"
#include "decorate/DemonTechZombie.txt"
#include "decorate/RocketZombie.txt"
#include "decorate/ShotgunZombie.txt"
#include "decorate/AssaultShotgunZombie.txt"
#include "decorate/SuperShotgunZombie.txt"
#include "decorate/QuadShotgunZombie.txt"
#include "decorate/AssaultRifleZombie.txt"
#include "decorate/Chaingunner.txt"

#include "decorate/DevilImp.txt"

Actor ZombieMan1 : ZombieMan {}

ACTOR PossessedZombieMan1 : PlayerPawn
{
  Health 20
  Speed 0.4
  Radius 20
  Height 56
  Mass 100
  PainChance 200
  Player.MorphWeapon ZombieRifle
  Player.DisplayName "ZombieMan"
  Player.WeaponSlot 1, ZombieRifle
  Player.MaxHealth 20
  Player.ViewHeight 41
  Player.SoundClass Brainless
  Player.JumpZ 10

  DamageFactor "MarineDamage", 0.6
  DamageFactor "BFGSplash", 0.6
  DamageFactor "MarineRLDamage", 0.6
  DamageFactor "CybRLDamage", 0.2
  DamageFactor "ImpBallDamage", 0
  DamageFactor "CacoDamage", 0
  DamageFactor "SkelDamage", 0
  DamageFactor "FatsoDamage", 0
  DamageFactor "KnightDamage", 0
  DamageFactor "ArachDamage", 0
  DamageFactor "viledamage", 0
  DamageFactor "Control", 0
  DamageFactor "ZombiesDamage", 0
  DamageFactor "Slime", 0
  
  +NOSKIN
  +NOPAIN
  -PICKUP
  -FRIENDLY
  +NOTARGET
  states
  {
  Spawn:
    POSS AB 10 
    Loop
  See:
    POSS A 0 
    POSS A 0 A_Jump (240,2)
    POSS A 0 A_PlaySoundEx ("grunt/active","body")
    POSS ABCD 4 
    Loop
  Missile:
    POSS E 3
    POSS F 6 bright
    POSS E 6 
    Goto spawn
  Death:
    POSS H 5 A_SpawnItemEx ("Clip",0,0,random(29,38),0,0,0,0,32,0)
    POSS I 5 A_PlaySoundEx ("grunt/death","Body")
    POSS J 5 A_NoBlocking
    POSS K 5 A_GiveToTarget ("MarineExp",2)
    POSS A 0 A_SpawnItemEx ("MDeathGhost",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
  DeathWait:
    POSS L 1 A_CheckSight("Removecorpse")
    loop
  XDeath:
    POSS M 0 A_SpawnItemEx ("Clip",0,0,random(29,38),0,0,0,0,32,0)
    POSS M 4 A_GiveToTarget ("MarineExp",2)
    POSS N 4 A_XScream
    POSS O 4 A_NoBlocking
    POSS P 0 A_SpawnItemEx ("MDeathGhost",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
    POSS PQRST 4
  XDeathWait:	
    POSS U 2 A_CheckSight("Removecorpse")
    loop
  Removecorpse:
    TNT1 A 0 Thing_Remove (0)
    stop
  }
}

// Weapon 


ACTOR ZombieRifle : Weapon 
{ 
  weapon.kickback 100
  attacksound "weapons/pistol"
  obituary "%o was killed by %k."
  +NOEXTREMEDEATH
  +WEAPON.NOALERT
  +WEAPON.AMMO_OPTIONAL
  +WEAPON.CHEATNOTWEAPON
  +INVENTORY.UNDROPPABLE

  states
  {
  Ready:
    TNT1 A 7 A_WeaponReady
    TNT1 A 0 A_JumpIfInventory ("Mskull",0,3)
    TNT1 A 7 A_WeaponReady
    TNT1 A 0 A_JumpIfInventory ("Mskull",3,1)
    Loop
    TNT1 A 0 HealThing(1)
    goto ready 
  Select:
	TNT1 A 1 A_Raise 
    loop
  DeSelect:
    TNT1 A 1 A_Lower
    Loop
  Fire:
    TNT1 A 3 A_GunFlash
    TNT1 B 3 bright A_FireBullets (0.5,0.5,1,8,"FBulletPuff",1)
	TNT1 A 0 A_FireCustomMissile ("tracer",random(2,2),0,2,3,0,0)
    TNT1 C 3 bright
    TNT1 A 0 A_Refire
    TNT1 A 5
    Goto Ready
  Flash:
    TNT1 A 3 A_Light0
    TNT1 A 3 A_Light1
    TNT1 A 3 A_Light2
    TNT1 A 0 A_Light0
    Stop
  AltFire:
    TNT1 A 5 ACS_ExecuteAlways (419,0,1)
	goto ready
  }
}
