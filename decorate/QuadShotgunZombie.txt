ACTOR PossessedQuadShotgunZombie : PossessedMonster
{ 
	Player.MorphWeapon "QuadShotgunZombieWeapon"

	Health 80
	Radius 20
	Height 56
	Speed 0.4
	Mass 100
	PainChance 100
	DropItem "PortableMedkit" 120
	DropItem "ArmorPlate" 120
	DropItem "AmmoPack" 80
	DropItem "HandGrenadeAmmo" 200 1
	DropItem "MineAmmo" 100 1
	DropItem "NewShell" 256 8
	+FLOORCLIP
	+MISSILEMORE
	SeeSound "superzombie/sight"
	PainSound "superzombie/pain"
	DeathSound "superzombie/death"
	ActiveSound "superzombie/active"
	AttackSound ""
	Obituary "%o was destroyed by a Quad Shotgunner."
	States
	{
	Spawn:
		QSZM A 10
		QSZI A 10
		Loop
	See:
		QSZM AABB 3
		TNT1 A 0 A_SpawnItem("FootStep",0,0,0,0)
		QSZM CCDD 3
		TNT1 A 0 A_SpawnItem("FootStep",0,0,0,0)
		Loop
	Missile:
		QSZM F 5 Bright
		QSZM E 12
		Goto See
    }
}

Actor QuadShotgunZombieWeapon : PossessedWeapon
{
	States
	{
	Ready:
		QSGN A 1 A_WeaponReady
		Goto Ready
	Fire:
		TNT1 A 0 A_JumpIfInventory("QSGZombieToken", 2, "Reload")
		TNT1 A 0 A_JumpIfInventory("QSGZombieToken", 1, "AltFire")
	QuadFire:
		TNT1 A 0 A_PlaySound("weapons/qsgfire")
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_Light1
		TNT1 A 0 Radius_Quake(10,5,0,1,0)
		TNT1 A 0 A_GiveInventory("QSGZombieToken", 2)	
		TNT1 AAAAAAAAAAAA 0 A_FireCustomMissile("MonsterTracer2",Random(6,-6),0,0,Random(6,-6))
		QS4F A 2 Bright A_FireBullets(15.4,10.6,26,5)
		QS4F B 3 Bright A_Light2
		TNT1 A 0 A_Light0
		QSGN CFG 1
		QSGN H 2
		QSER A 3
		QSER B 6
		QSGN GFEDC 2
		Goto ReloadFourShellsInstant
	AltFire:
		TNT1 A 0 A_PlaySound("weapons/qsgdoublefire")	
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_Light1
		TNT1 A 0 Radius_Quake(3,3,0,1,0)
		TNT1 A 0 A_JumpIfInventory("QSGZombieToken",2,"Reload")
		TNT1 A 0 A_JumpIfInventory("QSGZombieToken",1,"FireLower")
	FireUpper:
		TNT1 A 0 A_GiveInventory("QSGZombieToken", 1)
		TNT1 AAAAAA 0 A_FireCustomMissile("MonsterTracer2",Random(6,-6),0,0,Random(6,-6))
		QSGF A 2 Bright A_FireBullets(13.8,9.6,12,5)
		QSGF B 3 Bright A_Light2
		TNT1 A 0 A_Light0
		QREC ABCDE 1
		QSGN A 1
		Goto Ready
	FireLower:
		TNT1 A 0 A_GiveInventory("QSGZombieToken", 1)
		TNT1 AAAAAA 0 A_FireCustomMissile("MonsterTracer2",Random(6,-6),0,0,Random(6,-6))
		QSGF C 2 Bright A_FireBullets(13.8,9.6,12,5)
		QSGF D 2 Bright A_Light2
		TNT1 A 0 A_Light0
		QREC ABCDE 1
		QSGN A 1
		Goto Reload
	Reload:
		TNT1 A 0 A_JumpIfInventory("QSGZombieToken",2,"ReloadFourShells")
	ReloadTwoShells:
		QSGN SRQ 2
		QSGN P 1
		QSEX A 1
		QSEX B 1
		TNT1 AA 0 A_SpawnItemEx("SSGShellCasing",25,-2,25,Random(2,4),Random(-3,-6),Random(3,6),0)
		QSEX C 1
		QSGR I 12 A_PlayWeaponSound("weapons/qsgopen")
		QSGR JK 1
		QSGR LM 2
		TNT1 A 0
		TNT1 A 0
		QSGR N 3 A_PlayWeaponSound("weapons/qsgshellslide")	
		QSGR O 3
		QSGR P 2
		QSGR Q 1
		QSGN MNO 1
		QSGN P 1 A_PlayWeaponSound("weapons/qsgclose")
		QSGN QRS 2
		QSGN A 2
		TNT1 A 0 A_TakeInventory("QSGZombieToken", 1)
		Goto Ready
	ReloadFourShells:
		QSGN S 2
	ReloadFourShellsInstant:
		QSGN RQ 2
		QSGN PJ 1
		QSGN K 1
		TNT1 AAAA 0 A_SpawnItemEx("SSGShellCasing",25,-2,25,Random(2,4),Random(-3,-6),Random(3,6),0)
		QSGN L 1
		QSGR A 12 A_PlayWeaponSound("weapons/qsgopen")
		QSGR B 1
		QSGR CD 2
		TNT1 A 0
		TNT1 A 0
		QSGR E 3 A_PlayWeaponSound("weapons/qsgshellslide")
		QSGR F 3
		QSGR G 2
		QSGR H 1
		QSGR I 8
		QSGR JK 1
		QSGR LM 2
		TNT1 A 0
		TNT1 A 0
		QSGR N 3 A_PlayWeaponSound("weapons/qsgshellslide")	
		QSGR O 3
		QSGR P 2
		QSGR Q 1
		QSGN MNO 1
		QSGN P 1 A_PlayWeaponSound("weapons/qsgclose")
		QSGN QRS 2
		QSGN A 2
		TNT1 A 0 A_TakeInventory("QSGZombieToken", 2)
		Goto Ready
    }
}